MACRO (FINDFASTCALL)
  GET_PROPERTY(source_dir_set GLOBAL PROPERTY MYPACKAGE_SOURCE_DIR SET)
  IF (NOT ${source_dir_set})
    MESSAGE (WARNING "Cannot check fastcall, property MYPACKAGE_SOURCE_DIR is not set")
  ELSE ()
    IF (NOT C_FASTCALL_SINGLETON)
      GET_PROPERTY(source_dir GLOBAL PROPERTY MYPACKAGE_SOURCE_DIR)
      SET (_C_FASTCALL_FOUND FALSE)
      FOREACH (KEYWORD "__attribute__((regparm(3)))" "__attribute__((regparm(2)))" "__fastcall")
        SET(KEYWORD_AS_STRING "${KEYWORD}")
        MESSAGE(STATUS "Looking for ${KEYWORD_AS_STRING}")
        TRY_COMPILE (C_HAS_FASTCALL ${CMAKE_CURRENT_BINARY_DIR}
          ${source_dir}/fastcall.c
          COMPILE_DEFINITIONS -DC_FASTCALL=${KEYWORD_AS_STRING})
        IF (C_HAS_FASTCALL)
          MESSAGE(STATUS "Looking for ${KEYWORD} - found")
          SET (_C_FASTCALL ${KEYWORD})
          SET (_C_FASTCALL_FOUND TRUE)
          BREAK ()
        ENDIF ()
      ENDFOREACH ()
    ENDIF ()
    IF (_C_FASTCALL_FOUND)
      SET (C_FASTCALL "${_C_FASTCALL}" CACHE STRING "C fastcall keyword")
      MARK_AS_ADVANCED (C_FASTCALL)
    ENDIF ()
    SET (C_FASTCALL_SINGLETON TRUE CACHE BOOL "C fastcall check singleton")
    MARK_AS_ADVANCED (C_FASTCALL_SINGLETON)
  ENDIF ()
ENDMACRO()
