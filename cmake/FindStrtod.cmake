MACRO (FINDSTRTOD)
  GET_PROPERTY(source_dir_set GLOBAL PROPERTY MYPACKAGE_SOURCE_DIR SET)
  IF (NOT ${source_dir_set})
    MESSAGE (WARNING "Cannot check strtod, property MYPACKAGE_SOURCE_DIR is not set")
  ELSE ()
    IF (NOT C_STRTOD_SINGLETON)
      GET_PROPERTY(source_dir GLOBAL PROPERTY MYPACKAGE_SOURCE_DIR)
      SET (_C_STRTOD_FOUND FALSE)
      #
      # Test
      #
      FOREACH (KEYWORD "strtod" "_strtod" "__strtod")
        MESSAGE(STATUS "Looking for ${KEYWORD}")
        TRY_COMPILE (C_HAS_${KEYWORD} ${CMAKE_CURRENT_BINARY_DIR}
          ${source_dir}/strtod.c
          COMPILE_DEFINITIONS -DC_STRTOD=${KEYWORD})
        IF (C_HAS_${KEYWORD})
          MESSAGE(STATUS "Looking for ${KEYWORD} - found")
          SET (_C_STRTOD ${KEYWORD})
          SET (_C_STRTOD_FOUND TRUE)
          BREAK ()
        ENDIF ()
      ENDFOREACH ()
    ENDIF ()
    IF (_C_STRTOD_FOUND)
      SET (C_STRTOD "${_C_STRTOD}" CACHE STRING "C strtod implementation")
      MARK_AS_ADVANCED (C_STRTOD)
    ENDIF ()
    SET (C_STRTOD_SINGLETON TRUE CACHE BOOL "C strtod check singleton")
    MARK_AS_ADVANCED (C_STRTOD_SINGLETON)
  ENDIF ()
ENDMACRO()
